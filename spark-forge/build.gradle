plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id "xyz.wagyourtail.unimined" version "1.2.14"
}

tasks.withType(JavaCompile) {
    // override, compile targeting J8
    options.release = 8
}

unimined.minecraft {
    version "1.8.9"
    mappings {
        searge()
        mcp "stable", "22-1.8.9"
    }
    minecraftForge {
        loader "11.15.1.2318-1.8.9"
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    shade project(':spark-common')
}

processResources {
    inputs.property 'pluginVersion', project.pluginVersion
    inputs.property 'pluginDescription', project.pluginDescription
    filesMatching("mcmod.info") {
        expand(inputs.properties)
    }
}

shadowJar {
    archiveFileName = "spark-${project.pluginVersion}-legacy-forge-1.8.9-dev.jar"
    configurations = [project.configurations.shade]

    relocate 'net.kyori.adventure', 'me.lucko.spark.lib.adventure'
    relocate 'net.kyori.examination', 'me.lucko.spark.lib.adventure.examination'
    relocate 'net.kyori.option', 'me.lucko.spark.lib.adventure.option'
    relocate 'net.bytebuddy', 'me.lucko.spark.lib.bytebuddy'
    relocate 'com.google.protobuf', 'me.lucko.spark.lib.protobuf'
    relocate 'org.objectweb.asm', 'me.lucko.spark.lib.asm'
    relocate 'one.profiler', 'me.lucko.spark.lib.asyncprofiler'
    relocate 'me.lucko.bytesocks.client', 'me.lucko.spark.lib.bytesocks'
    relocate 'org.java_websocket', 'me.lucko.spark.lib.bytesocks.ws'

    project.applyExcludes(delegate)
}

remapJar {
    inputFile = tasks.shadowJar.archiveFile
    dependsOn tasks.shadowJar
    archiveFileName = "spark-${project.pluginVersion}-legacy-forge-1.8.9.jar"
}

artifacts {
    archives remapJar
    shadow shadowJar
}
